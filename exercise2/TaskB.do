**********  Task B **********  Data ********** 
clear
cd /Users/stanza/documents/github/etc4420
set more off
capture log close
log using TaskB.log,replace
use "SS hilda.dta", replace

//Introduction and data cleaning.
//We are insterested in the factors determining the weekly wage for the whole 
//Australian population, using the wage data of those who are currently working.
//First, we check the data by looking at the summary statistics. 
sum
///In this dataset, we have variables relating personal earnings, age, gender,
//current employment status, eduction level, number of dependent children,
//having concenssion card or not, self-reported health, and marital status.
//All variables are discrete or dummy variables except income related variables.

//There are a lot of missing values in logincome, which is caused by the zero
//in disposable income. There are also missing values in 'lfp' because many
//people did not respond to the employment status question in the survey. Same 
//with 'esempst' which is a more detailed employment status variable, 'scage'
//which indicating age group,  'male', 'edhigh' which is the highest education 
//level, 'concession' and 'married'.
//Since we want to study the weekly wage, we will use 'wscei' as our dependent 
//variable. So we check its histogram.
histogram wscei
//From this histogram we can see 'wscei' has a lot of zero values. So we create
//a new variable call pos_wscei for the non-zero values in wscei and check how 
//they are distributed.
gen pos_wscei=wscei if wscei>0
histogram pos_wscei
//The distribution of all positive wscei values is a positively skewed
//distribution, where it may be better to do log transformation to it and 
//then analyse. But if so we will have much less observations since a lot of 
//missing values will be generated by this log-transformation. 
//This could be a problem for Tobit model.
//Therefore, we will use wscei directly as our dependent variable.
//Also we want to keep as many observations as we could, 
//so we will not use any employment status related variables in this data set 
//directly. Instead, we will generate a new dummy variable called 'working' 
//indicating if a person is working or not given the value of his/her 'wscei'
//as our selection variable.
//We will also use 'male', detailed 'age' band variables (the youngest group is
//the reference level) since we have relatively large sample size, 
//and four 'education' levels (less than year 12 will be omitted as the
//reference) as the determinants for the outcome equation. Because age and
//education are obviously have strong correlation with one's wage. To confirm,
//we do cross tabulation for age and wage.
tab scage, summarize(wscei)
//From this cross tabulation, we can see strong relationship between 'age' and 
//'wscei'. Wage is increasing with age before 45-54, and dreasing with it after.
//Unfortunately, gender is assumed to still have some impact as well.
//'Marital status' and 'dependent children' will be included in the selection 
//equation as extra IVs. Because these two factors will definitely have impact
//on one person's employment status but assumed to be irrelevant to one's wage.

gen working = wscei != 0
lab var working "=1 for working people, =0 otherwise"

//Check the new variable by looking at its summary statistics.
tab working
//In this subset, there are 8216 working people indicated by 'working', while 
//9414 non-working people. This 'working' variable maybe not necessarily accurate
//given the way we generated it. So we check the cross tabulation of it with 
//other employment status variables.
tab esbrd working
//We can see there are some mismatch between the two, since some 'employee' 
//indicated by 'esbrd' are grouped as 0 in 'working'. This may because some 
//employees did not report the weekly wage. As far as our assignment concerned,
//we will assume the working status are mostly correctly specified and use it in 
//our further analysis.

**********  Task B **********  Questions ********** 

set seed 27886913
sample 12000, count

//Check data again after sampling.
sum
tab working
//Random sampling works fine. Now we have 6361 non-working people while 5639 
//working people in this subset (indicated by 'working').

//Estimate a Heckman sample selection model using Two-step Heckit Estimator
//Task B Question 1

heckman wscei age1819 age2021 age2224 age2534 age3544 age4554 age5564 age6574 /*
*/age75above male bachabv dipcert year12, select(working =age1819 age2021 /*
*/age2224 age2534 age3544 age4554 age5564 age6574  age75above male bachabv /*
*/dipcert year12 married depkid) twostep
estimate store heckman_2step

//Estimate a Heckman sample selection model using MLE
//Task B Question 1

heckman wscei age1819 age2021 age2224 age2534 age3544 age4554 age5564 age6574 /*
*/age75above male bachabv dipcert year12, select(working =age1819 age2021 /*
*/age2224 age2534 age3544 age4554 age5564 age6574  age75above male bachabv /*
*/dipcert year12 married depkid)

estimate store heckman_mle
estimate table heckman_2step heckman_mle

//As we can see, the estimated coefficients are very different for these two
//methods in the outcome equation. I think the reason for that maybe relates to
// what we mentioned above, the distribution of the error may not be normal is,
//and this may affect our estimation. 
//The estimated standard deviations are very large. Hence the estimates are
//very sensitive to the change in estimating methods.

//What's more, we can see the estimated rho is negative. Intuitively, it is more
//reasonable to be positive. The residuals in the outcome equation and the 
//selection equation maybe positively correlted. Because the unobserved factors
//that make people working are more likely to also make people earn more.
//I think this may due to the lack of good IVs, since 'depkid' may be related 
//with how much one can earn. For example, for someone who works on hourly basis
//having dependent kids is more likely to reduce his/her working hours.
//Or it could also be X variables we put in the regressions taking
//too much information in residuals which over-compensated the issue. 
//Or even maybe our dataset is not a good one.

//Estimate the marginal effects for E('wscei') and E('wscei'|working=1) based on 
//the MLE model above.
//Task B Question 2

//marginal effects at means for E('wscei'), unconditional ME
quietly heckman wscei age1819 age2021 age2224 age2534 age3544 age4554 age5564 age6574 /*
*/age75above male bachabv dipcert year12, select(working =age1819 age2021 /*
*/age2224 age2534 age3544 age4554 age5564 age6574  age75above male bachabv /*
*/dipcert year12 married depkid)
margins, dydx(*) atmean

//marginal effects at means for E('wscei'|working=1), conditional ME
margins, dydx(*) predict(ycond) atmean
//Continued from our previous discussion about the negative rho, we can see 
//some weird results from these two marginal effects. For instance, the marginal 
//effect of achieving bachelor degree is larger on the working group which is 
//582.4258 while 558.4575 on the whole population. In reality, we think
//education should have bigger marginal effect on the whole population than 
//the working people since employees are often have higher education level than
//unemployees. This result is caused by the fact that our estimated rho is
//negative.

//Estimate a Tobit model for 'wscei' with a left censoring point of wscei=0.
//Task B Question 3
//We use all variables as explanatory variables including married and depkid.

tobit wscei age1819 age2021 age2224 age2534 age3544 age4554/*
*/ age5564 age6574 age75above male bachabv dipcert year12 married depkid, ll

//Unconditional marginal effects at means for E('wscei') for Tobit 
margins, dydx(*) atmean
//The Î² coefficients themselves measure how the unobserved variable y* changes 
//with respect to changes in the regressors. That's why these marginal effects
//are the same with our coefficient estimates in Tobit model.


//Conditional marginal effects at means for E('wscei'|working=1) for Tobit
margins, dydx(*) predict(ystar(0,.)) atmean

//Heckman SS model prodicts positive marginal effects of all variables for the
//unconditional group; while 'age above75' and having depkid has negative ME for
//working people.
//Tobit suggests negative ME of age above 65 and having depkid for both groups.

//Both of the models agree 'male' has a positive effect on wage, no matter 
//conditionally or unconditionally. As well as 'married' and education. 

//Theoritically, Tobit treats the data as censored, considering a zero-wage for 
//anyone who is not in the labor force, while the Heckman model treats data as
//truncated. These two are very different methods, unsurprisingly, the marginal 
//effects given by them are quite different. For example, Tobit estimates the 
//marginal effects of 'having bachelor degree' is larger on the unconditional
//group than the working group. This opposites what we observe from the Heckman
//sample selection model. 
//On the other hand, the non-normal distribution may has some impact on our 
//estimation as well. So these results may not be very reliable.


log close 








